#pragma kernel CSMain
#include "Includes/Noise.compute"
#include "TerrainUtils.compute"

uint N;
uint seam;
uint textureRes;
float size;

int2 chunk;

RWTexture2D<float> HeightMap;

int seed;

float falloff_radius;
float2 falloff_center;
float falloff_minHeight;
float falloff_maxHeight;
float falloff_falloffPower;

int base_octaves;
float base_amplitude;
float base_scale;
float base_persistence;
float base_lacunarity;

int ridge_octaves;
float ridge_amplitude;
float ridge_scale;
float ridge_persistence;
float ridge_lacunarity;

float ridgeOffset;
bool displayRidge;

float ridge(float baseHeight, float ridgeHeight)
{
    return 2 * (ridgeHeight - abs(baseHeight - ridgeHeight));
}

float octaveNoise(float2 pos, int octaves, float persistence, float lacunarity)
{
    if (octaves <= 0) return 0;

    float a = 1;
    float s = 1;

    float noiseSum = 0;
    float ampSum = 0;

    pos += ((0.123456 + seed) * float2(-12.013, 64.134)) % float2(8991.4546, 1343.351);

    for (int o = 0; o < octaves; o++)
    {
        noiseSum += a * snoise(float3(s * pos, 0));
        ampSum += a;

        a *= persistence;
        s *= lacunarity;
    }

    return noiseSum / ampSum;
}

float calculateFalloff(float2 pos)
{
    float dist = length(pos - falloff_center);

    float t = pow(1 - saturate(dist / falloff_radius), falloff_falloffPower);

    float tt = t * t * (3 - 2 * t);
        
    return t * (falloff_maxHeight - falloff_minHeight) + falloff_minHeight;
}

float basicRidge(float x)
{
    return 1 - abs(2 * x - 1);
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= textureRes || id.y >= textureRes) return;

    float2 pos = calculatePosition(id.xy - seam, N, size);

    pos += size * chunk;

    float falloff = calculateFalloff(pos);

    float ridgeNoise = ridge_amplitude * octaveNoise(ridge_scale * pos, ridge_octaves, ridge_persistence, ridge_lacunarity);
    float ridgeHeight = ridgeNoise + ridgeOffset;
    
    float baseHeight = base_amplitude * octaveNoise(base_scale * pos, base_octaves, base_persistence, base_lacunarity);

    if (displayRidge)
    {
        HeightMap[id.xy] = ridgeHeight;
        return;
    }

    //HeightMap[id.xy] = falloff + ridge(baseHeight, ridgeHeight);

    HeightMap[id.xy] = falloff + baseHeight + basicRidge(ridgeHeight);
}
